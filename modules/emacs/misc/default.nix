{ config, lib, pkgs, ... }:
with pkgs.unstable.commonutils;
with lib;

let
  cfg = config.ide.emacs.misc;
in
{
  options = {
    ide.emacs.misc = {
      enable = mkOption {
        type = types.bool;
        default = false;
        description = "Whether to enable emacs misc extensions.";
      };
    };
  };

  config = mkMerge [
    (mkIf cfg.enable {
      assertions = [
        {
          assertion = config.ide.emacs.core.enable;
          message = "emacs/misc: core configuration must be enabled.";
        }
        {
          assertion = config.ide.emacs.edit.enable;
          message = "emacs/misc: emacs/edit must be enabled.";
        }
      ];

      ide.emacs.core.extraPackages = epkgs: [
        epkgs.flycheck
        epkgs.avy-flycheck
        epkgs.hl-todo
        epkgs.copy-as-format
        epkgs.format-all
        epkgs.ini-mode
        epkgs.markdown-mode
      ];
      ide.emacs.core.customKeymaps = {
        "custom-formatting-map" = "C-c f";
      };
      ide.emacs.core.config =
        builtins.readFile ./elisp/misc.el +
        optionalString (!config.ide.emacs.core.treesitter.enable) (builtins.readFile ./elisp/non-ts.el) +
        optionalString (config.ide.emacs.core.treesitter.enable) (builtins.readFile ./elisp/ts.el);
      ide.emacs.core.treesitter.grammars = {
        markdown = "https://github.com/ikatyang/tree-sitter-markdown";
      };
      ide.emacs.core.treesitter.modeRemappings = {
        markdown-mode = "markdown-ts-mode";
      };
    })
  ];
}
